task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.3'
    }
}
apply plugin: 'com.android.library'

repositories {
    mavenCentral()
}

dependencies {
    def supportv4Project = ':support-v4'
    def androidasyncProject = ':AndroidAsync:AndroidAsync'

    def useLocalV4 = false
    def useLocalAndroidAsync = false
    allprojects {
        rootProject.allprojects.project.each {
            if (it.path == androidasyncProject) {
                useLocalAndroidAsync = true
            }
            if (it.path == supportv4Project) {
                useLocalV4 = true
            }
        }
    }

    if (useLocalAndroidAsync) {
        println 'Using local AndroidAsync library in ' + project.path
        compile project(androidasyncProject)
    } else {
        compile 'com.koushikdutta.async:androidasync:1.4.+'
    }

    if (useLocalV4) {
        println 'Using local support-v4 library in ' + project.path
        compile project(supportv4Project)
    } else {
        compile 'com.android.support:support-v4:21.+'
    }

    compile 'com.google.code.gson:gson:2.3'
}

android {
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'

            java {
                srcDir 'src/'
            }
        }
        androidTest.java.srcDirs=['test/src/']
        androidTest.res.srcDirs=['test/res/']
        androidTest.assets.srcDirs=['test/assets/']
    }

    android {
        lintOptions {
            abortOnError false
        }
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
    }

    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    publishNonDefault true
}

// upload to maven task
if (System.getenv().I_AM_KOUSH == 'true') {
    apply from: 'https://raw.githubusercontent.com/koush/mvn-repo/master/maven.gradle'

    apply plugin: 'maven'
    apply plugin: 'signing'

    afterEvaluate { project ->
        uploadArchives {
            repositories {
                mavenDeployer {
                    pom*.whenConfigured { pom ->
                        pom.dependencies.removeAll { dep -> dep.artifactId == 'support-v4' }
                    }
                }
            }
        }
    }
}
