task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

buildscript {
    repositories {
        maven { url 'http://repo1.maven.org/maven2' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:+'
    }
}

apply plugin: 'com.android.library'

repositories {
    mavenCentral()
}

dependencies {
    def supportv4Project = ':support-v4'
    def androidasyncProject = ':AndroidAsync:AndroidAsync'
    def artifactPlayServiceBase = 'com.google.android.gms:play-services-base:+'

    def useLocalV4 = false
    def useLocalAndroidAsync = false
    allprojects {
        rootProject.allprojects.project.each {
            if (it.path == androidasyncProject) {
                useLocalAndroidAsync = true
            }
            if (it.path == supportv4Project) {
                useLocalV4 = true
            }
        }
    }

    if (useLocalAndroidAsync) {
        println 'Using local AndroidAsync library in ' + project.path
        compile project(androidasyncProject)
    } else {
        compile 'com.koushikdutta.async:androidasync:+'
    }

    if (useLocalV4) {
        println 'Using local support-v4 library in ' + project.path
        compile project(supportv4Project)
        compile (artifactPlayServiceBase) {
            exclude group: 'com.android.support', module: 'support-v4'
        }
    } else {
        compile 'com.android.support:support-v4:21.+'
        compile artifactPlayServiceBase
    }

    compile 'com.google.code.gson:gson:2.3'
}

android {
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'

            java {
                srcDir 'src/'
            }
        }
        androidTest.java.srcDirs=['test/src/']
        androidTest.res.srcDirs=['test/res/']
        androidTest.assets.srcDirs=['test/assets/']
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        targetSdkVersion 21
        minSdkVersion 9
    }

    compileSdkVersion 21
    buildToolsVersion '21.1.1'
}

// upload to maven task
if (System.getenv().I_AM_KOUSH == 'true') {
    apply from: 'https://raw.githubusercontent.com/koush/mvn-repo/master/maven.gradle'

    apply plugin: 'maven'
    apply plugin: 'signing'

    afterEvaluate { project ->
        uploadArchives {
            repositories {
                mavenDeployer {
                    pom*.whenConfigured { pom ->
                        pom.dependencies.removeAll { dep -> dep.artifactId == 'support-v4' }
                    }
                }
            }
        }
    }
}
